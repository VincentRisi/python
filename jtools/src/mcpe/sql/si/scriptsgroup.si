//  &Revision: 98 &
//  &Date: 2007-06-29 09:32:15 +0200 (Fri, 29 Jun 2007) &

DATABASE VLAB 
PACKAGE  vlab.mcpe
OUTPUT   scriptsgroup
SERVER   LV01
SCHEMA   DBUD00
USERID   dbu
PASSWORD dbudev

TABLE ScriptGroup
  ScriptName   char(64)
  GroupId      Char(16)
  AccessType   byte    (ReadWrite=0 Read=1 None=2)
  USId         Char(16)
  TmStamp      DateTime

KEY StaffGroupKey PRIMARY
  ScriptName
  GroupId

LINK Grps GroupId

PROC Insert 
PROC Update 
PROC SelectAll
PROC SelectOne
PROC DeleteOne 

PROC ListAll 
OUTPUT (1000)
  ScriptName Char(64)
  GroupId Char(16)
  AccessType byte
sqlcode
  SELECT ScriptName
       , GroupId
       , AccessType
  FROM ScriptGroup
  ORDER BY ScriptName
         , GroupId
ENDCODE

PROC InsertScripts 
INPUT
  GroupId      Char(16)
  AccessType   byte    
  USId         Char(16)
sqlcode
  insert into ScriptGroup
    (select unique name, :GroupId, :AccessType, :USId, SYSDATE
     from ScriptVersion
     where name in (&ScriptList(40960)))
ENDCODE

PROC DeleteScripts 
INPUT
  GroupId      Char(16)
sqlcode
  delete from ScriptGroup
  where GroupId = :GroupId
  and ScriptName in (&ScriptList(4096))
ENDCODE

PROC UpdateScriptsAccess 
INPUT
  GroupId      Char(16)
  AccessType   byte    
  USId         Char(16)
sqlcode
  update ScriptGroup set AccessType = :AccessType, USId = :Usid
  where GroupId = :GroupId
  and ScriptName in (&ScriptList(40960))
ENDCODE

PROC InsertGroups 
INPUT
  ScriptName   char(64)
  AccessType   byte    
  USId         Char(16)
sqlcode
  insert into ScriptGroup
    (select unique :ScriptName, GroupId, :AccessType, :USId, SYSDATE
     from Grps
     where name in (&GroupList(40960)))
ENDCODE

PROC DeleteGroups 
INPUT
  ScriptName   char(64)
sqlcode
  delete from ScriptGroup
  where GroupId in (&GroupList(4096))
  and ScriptName = :ScriptName
ENDCODE

PROC UpdateGroupsAccess 
INPUT
  ScriptName   char(64)
  AccessType   byte    
  USId         Char(16)
sqlcode
  update ScriptGroup set AccessType = :AccessType, USId = :Usid
  where GroupId in (&GroupList(40960))
  and ScriptName = :ScriptName
ENDCODE

sqldata
  insert into ScriptGroup 
    (select unique name, 'INTRSYS', 0, 'PUTTYIDE', SYSDATE 
     from ScriptVersion 
     where name like 'DB_%' 
     or name like 'SYS_%');
  insert into ScriptGroup 
    (select unique name, 'NEDPAYM', 0, 'PUTTYIDE', SYSDATE 
     from ScriptVersion 
     where name not like 'DB_%' 
     and name not like 'SYS_%'
     and name not like 'ZAR_%');
  insert into ScriptGroup 
    (select unique name, 'ZARPAYM', 0, 'PUTTYIDE', SYSDATE 
     from ScriptVersion 
     where name like 'ZAR_%');
  commit;
enddata

